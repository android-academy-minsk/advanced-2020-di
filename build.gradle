// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.kotlinVersion = '1.3.61'
    ext.gradleVersion = '3.6.0'

    repositories {
        google()
        jcenter()

    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {
    minApiVersion = 19
    maxApiVersion = 28
    aptVersion = "28.0.3"

    timberVersion = "4.6.1"

    junitVersion = "4.12"

    appcompatVersion = "1.1.0"
    materialVersion = "1.1.0"
    constraintVersion = "1.1.3"

    gsonVersion = "2.8.5"

    daggerVersion = "2.24"

    coroutinesAndroidVersion = '1.3.2'
    lifecycleVersion = '2.2.0'
    livedataVersion = '2.2.0'
}

subprojects {

    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {

        compileSdkVersion maxApiVersion
        buildToolsVersion aptVersion

        defaultConfig {

            minSdkVersion minApiVersion
            targetSdkVersion maxApiVersion

            if (project.name == 'app') {
                applicationVariants.all { variant ->
                    variant.outputs.all {
                        outputFileName = "app-${variant.name}-${variant.versionName}.apk"
                    }
                }
            }
        }

    }

    dependencies {

        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

        // Logging
        implementation "com.jakewharton.timber:timber:$timberVersion"

        implementation 'org.kodein.di:kodein-di-generic-jvm:6.4.0'

        // Unit tests
        testImplementation "junit:junit:$junitVersion"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
